//vday_mob_manager

021-3.gat,120,130,20,21|monster|AngrySeaSlime|1109,6,150000,100000,VdayMob021-3::On1109
021-3.gat,53,130,20,21|monster|AngrySeaSlime|1109,6,150000,100000,VdayMob021-3::On1109
021-3.gat,128,69,27,7|monster|AngrySeaSlime|1109,6,150000,100000,VdayMob021-3::On1109
021-3.gat,149,129,2,15|monster|Troll|1054,2,600000,100000,VdayMob021-3::On1054
021-3.gat,28,52,3,20|monster|Troll|1054,2,600000,100000,VdayMob021-3::On1054

021-3.gat,0,0,0|script|VdayMob021-3|-1,
{
On1109:
    set @mobID, 1109;
    callsub S_Mobcount;
    callfunc "MobPoints";
    end;

On1054:
    set @mobID, 1054;
    callsub S_Trolls;
    callfunc "MobPoints";
    end;

S_Mobcount:
    set @vday_couple1_state, ((VDAYTIME & $@VDAY_SUBSTATE1_MASK) >> $@VDAY_SUBSTATE1_SHIFT);
    if (@vday_couple1_state != $@VDAY_C1HELPING_STATE) goto L_Return;
    set @ring, @ring + 1;
    if (@ring == 1) message strcharinfo(0), "Maybe one of those electric seaslimes picked the Ring up.";
    if (@ring < ($@VDAY_RING_MIN + (BaseLevel / 13))) goto L_Return;
    if (rand(8) != 0) goto L_Return;
    message strcharinfo(0), "Oh this is pointless, I should look somewhere else;";
    set @vday_couple1_state, $@VDAY_C1FOUND0_STATE;
    callfunc "VdaySetCouple1";
    set @ring, 0;
    return;

S_Trolls:
    set @vday_couple1_state, ((VDAYTIME & $@VDAY_SUBSTATE1_MASK) >> $@VDAY_SUBSTATE1_SHIFT);
    if (@vday_couple1_state != $@VDAY_C1FOUND0_STATE) goto L_Return;
    set @ring, @ring + 1;
    if (@ring == 1) message strcharinfo(0), "Mmh these trolls look like they just love to hoard sparkling things";
    if (rand(3) != 0) goto L_Return;
    set @vday_couple1_state, $@VDAY_C1FOUND_STATE;
    callfunc "VdaySetCouple1";
    message strcharinfo(0), "Indeed, seems like this is the ring Noah has asked for.";
    getitem $@VDAY_RING$, 1;
    set @ring, 0;
    return;

L_Return:
    return;
}
