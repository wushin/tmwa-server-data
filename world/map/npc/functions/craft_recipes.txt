// Basic craft functionality

// ------------------------------------------------------------
// Craft Menu
// ------------------------------------------------------------
function|script|CraftMenu|{
    set @HasSkill, getskilllv(@RequiredSkill);
    set @ListVar, 0;
    set $@MesReqsCheck, 0;
    // Set Used if exists
    if(CRAFT_ONE == @RequiredSkill)
        set @OldCraftXP, CRAFT_ONE_XP;
    if(CRAFT_TWO == @RequiredSkill)
        set @OldCraftXP, CRAFT_TWO_XP;

    mes "["+@CraftObject$+"]";
    mes "\"Welcome to the "+@CraftObject$+".\"";
    next;

    if(@HasSkill == 0)
        goto L_Learn;

    if(@HasSkill > 0)
        mes "\"Lets look in our recipe book.\"";
        callfunc(@RequiredSkillName$ + "Recipes");
        set @RecipeCnt, getarraysize(@RecipesCho$);
        if (@RecipeCnt > 6)
            goto L_MultiMenus;
        goto L_SimpleMenu;
    goto L_FinClear;

    L_MultiMenus:
        mes "\"What shall we make?\"";
        next;
        menu 
           "<<< Back", L_RecipesLess,
           @RecipesCho$[(@ListVar + 0)],L_CraftOne,
           @RecipesCho$[(@ListVar + 1)],L_CraftTwo,
           @RecipesCho$[(@ListVar + 2)],L_CraftThree,
           @RecipesCho$[(@ListVar + 3)],L_CraftFour,
           @RecipesCho$[(@ListVar + 4)],L_CraftFive,
           "Forward >>>", L_RecipesMore;
        goto L_FinClear;

    L_RecipesMore:
        set @ListVar, @ListVar + 5;
        if(@ListVar >= @RecipeCnt)
            set @ListVar, @RecipeCnt - 5;
        goto L_MultiMenus;

    L_RecipesLess:
        set @ListVar, @ListVar - 5;
        if(@ListVar < 0)
            set @ListVar, 0;
        goto L_MultiMenus;

    L_SimpleMenu:
        mes "\"What shall we make?\"";
        menu
           @RecipesCho$[(@ListVar + 0)],L_CraftOne,
           @RecipesCho$[(@ListVar + 1)],L_CraftTwo,
           @RecipesCho$[(@ListVar + 2)],L_CraftThree,
           @RecipesCho$[(@ListVar + 3)],L_CraftFour,
           @RecipesCho$[(@ListVar + 4)],L_CraftFive;
        goto L_FinClear;

    L_CraftOne:
         set @menuItem, @ListVar;
         goto L_CraftItem;
    L_CraftTwo:
         set @menuItem, @ListVar + 1;
         goto L_CraftItem;
    L_CraftThree:
         set @menuItem, @ListVar + 2;
         goto L_CraftItem;
    L_CraftFour:
         set @menuItem, @ListVar + 3;
         goto L_CraftItem;
    L_CraftFive:
         set @menuItem, @ListVar + 4;
         goto L_CraftItem;

    L_Learn:
        message strcharinfo(0), "You need to learn "+@RequiredSkillName$+".";
        goto L_FinClear;

    L_CraftItem:
        callfunc(@RecipesIdx$[@menuItem]);
        set $@ReqsCheck, 0;
        set $@ReqIngCnt, 0;
        goto L_StartReqChecks;

    L_StartReqChecks:
        set $@ReqIngCnt, getarraysize(@ReqIng$);
        if ($@ReqIngCnt > $@ReqsCheck) 
             goto L_CheckReq;
        getinventorylist;
        if ((checkweight(@ItemName$, @ItemNum) == 0) || (@inventorylist_count == 100))
             goto L_NoSpace;
        set $@DelReqsCheck, 0;
        set $@DelReqIngCnt, 0;
        goto L_StartDelReqs;

    L_CheckReq:
        set $@ItemCount, countitem(@ReqIng$[$@ReqsCheck]);
        if($@ItemCount < @ReqIngNum[$@ReqsCheck])
            goto L_ReqIngMes;
        set $@ReqsCheck, $@ReqsCheck+1;
        goto L_StartReqChecks;

    L_StartDelReqs:
        set $@DelReqIngCnt, getarraysize(@ReqIng$);
        if ($@DelReqIngCnt == 0)
            goto L_GetItem;
        if ($@DelReqIngCnt > $@DelReqsCheck)
            goto L_DelReqIng;
        goto L_GetItem;

    L_DelReqIng:
        delitem @ReqIng$[$@DelReqsCheck], @ReqIngNum[$@DelReqsCheck];
        set $@DelReqsCheck, $@DelReqsCheck+1;
        goto L_StartDelReqs;

    L_GetItem:
        getitem @ItemName$, @ItemNum;
        misceffect FX_GETITEM, strcharinfo(0);
        goto L_FinClear;

    L_ReqIngMes:
        message strcharinfo(0), "You are missing Ingredients. The Following are required to make "+@CommonName$+": ";
        goto L_StartBuildMsg;

    L_StartBuildMsg:
        set $@ReqIngCnt, getarraysize(@ReqIng$);
        if($@ReqIngCnt > $@MesReqsCheck)
            goto L_BuildMsg;
        goto L_FinClear;

    L_BuildMsg:
        message strcharinfo(0), @ReqIngNum[$@MesReqsCheck]+" "+@ReqIngCn$[$@MesReqsCheck];
        set $@MesReqsCheck, $@MesReqsCheck+1;
        goto L_StartBuildMsg;

    L_NoSpace:
        message strcharinfo(0), "This item will be too heavy for you to construct.";
        goto L_FinClear;

    L_FinClear:
        set $@MesReqsCheck, 0;
        set $@DelReqsCheck, 0;
        set $@DelReqIngCnt, 0;
        set $@ReqsCheck, 0;
        set $@ReqIngCnt, 0;
        set @CommonName$, "";
        set @ItemName$, "";
        set ZenyAmt, 0;
        cleararray @ReqIng$,"",getarraysize(@ReqIng$);
        cleararray @ReqIngCn$,"",getarraysize(@ReqIngCn$);
        cleararray @ReqCnt$,"",getarraysize(@ReqCnt$);
        return;
}
