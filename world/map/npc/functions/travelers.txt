// Travelers
// Authors: Wombat, Wushin

// Multiple Travelers
// Cannot access until found (Player Bitmask)
// Hub-Spoke
// --Sub-Spoke
// Costs Zeny

// Contains all warp points to find
botcheck.gat,27,28,0|script|#TravelConfig|-1,
{
OnInit:
    // TravelFound
    set $@tut_bit, (1 << 2);
    set $@druid_tree_bit, (1 << 3);
    set $@graveyard_bit, (1 << 4);
    set $@magic_house_bit, (1 << 5);
    set $@terranite_cave_bit, (1 << 6);
    set $@tulimshar_bit, (1 << 7);
    set $@blue_sage_bit, (1 << 8);
    set $@hurnscald_bit, (1 << 9);
    end;
}

function|script|TravelerTut|,
{
    //Explain Travelers
    mes "["+@NpcName$+"]";
    mes "\"We travelers are found all over the world. Once you have found another traveler, we can create a link and send you between us.\"";
    next;
    if (TravelFound & $@tut_bit)
        goto L_Return;
    goto L_SetBit;

L_SetBit:
    set TravelFound, TravelFound | $@tut_bit;
    goto L_Return;

L_Return:
    return;
}

function|script|Traveler|,
{
    if (TravelFound & $@tut_bit)
        goto L_Main;
    goto L_TravelTut;

L_Main:
    if (TravelFound & @NpcTravelBit)
        goto L_BitTravelSet;
    goto L_SetTravelBit;

L_BitTravelSet:
    if (BaseLevel < 45)
        goto L_LowerCost;

    set @CostDruidTree, 1500;
    set @CostGraveyard, 1500;
    set @CostMagicHouse, 1000;
    set @CostTerraniteCave, 1500;
    set @CostTulimshar, 1000;
    set @CostBlueSage, 1500;
    goto L_Start;

L_TravelTut:
    callfunc "TravelerTut";
    goto L_Main;

L_SetTravelBit:
    mes "["+@NpcName$+"]";
    mes "\"Uplink Set.\"";
    next;
    set TravelFound, TravelFound | @NpcTravelBit;
    goto L_BitTravelSet;

L_LowerCost:
    set @CostDruidTree, 750;
    set @CostGraveyard, 750;
    set @CostMagicHouse, 500;
    set @CostTerraniteCave, 750;
    set @CostTulimshar, 500;
    set @CostBlueSage, 500;
    goto L_Start;

L_Start:
    mes "["+@NpcName$+"]";
    // Bleh, Edit text
    mes "\"Greetings.  I am Diryn the Traveler, Assistant Councilor of Tulimshar and teleporter extraordinaire.  Tulimshar is expanding our influence throughout the world, so I was sent here to aid all who are willing and able to take on the threats to our interests.\"";
    next;
    mes "\"The monsters of the world have grown out of control, harming people, trade and travel.  Needless to say, Tulimshar is not happy with this problem.  Our solution: Rally those powerful enough to combat the monster threat and send them to hot spots to challenge the monster threat head on.  However, teleportation to these areas is not only extremely dangerous, but also highly expensive.   Do you think you got what it takes?\"";
    menu
        "I sure do!", L_TravelYes,
        "No, thank you.", L_TravelNo;

L_TravelYes:
    mes "["+@NpcName$+"]";
    mes "\"Excellent.  I can send you to many places, but again, the fees are large. Where shall I send you?\"";
    menu
        "Druid Tree (" + @CostDruidTree + " GP)", L_TravelDruidTree,
        "Graveyard (" + @CostGraveyard + " GP)", L_TravelGraveyard,
        "Magic House (" + @CostMagicHouse + " GP)", L_TravelMagicHouse,
        "Terranite Cave (" + @CostTerraniteCave + " GP)", L_TravelTerranite,
        "Tulimshar Town Square (" + @CostTulimshar + " GP)", L_TravelTulimshar,
        "Sage Nikolai's Mansion (" + @CostBlueSage + " GP)", L_TravelBlueSage,
        "Hurnscald (" + @CostHurnscald + " GP)", L_TravelHurnscald,
        "I'm not interested.", L_TravelNo;

L_TravelDruidTree:
    if (@NpcTravelBit == $@druid_tree_bit)
        goto L_AlreadyThere;
    if (!(TravelFound & $@druid_tree_bit))
        goto L_NoFound;
    if (Zeny < @CostDruidTree)
        goto L_NoMoney;
    mes "["+@NpcName$+"]";
    mes "\"Be fearless!\"";
    close2;
    set Zeny, Zeny - @CostDruidTree;
    warp "005-1.gat",76,36;
    goto L_Clearvars;

L_TravelGraveyard:
    if (!(TravelFound & $@graveyard_bit))
        goto L_NoFound;
    if (Zeny < @CostGraveyard)
        goto L_NoMoney;
    mes "["+@NpcName$+"]";
    mes "\"Be fearless!\"";
    close2;
    set Zeny, Zeny - @CostGraveyard;
    warp "027-1.gat",84,86;
    goto L_Clearvars;

L_TravelMagicHouse:
    if (!(TravelFound & $@magic_house_bit))
        goto L_NoFound;
    if (Zeny < @CostMagicHouse)
        goto L_NoMoney;
    mes "["+@NpcName$+"]";
    mes "\"Be fearless!\"";
    close2;
    set Zeny, Zeny - @CostMagicHouse;
    warp "013-1.gat",45,92;
    goto L_Clearvars;

L_TravelTerranite:
    if (!(TravelFound & $@terranite_cave_bit))
        goto L_NoFound;
    if (Zeny < @CostTerraniteCave)
        goto L_NoMoney;
    mes "["+@NpcName$+"]";
    mes "\"Be fearless!\"";
    close2;
    set Zeny, Zeny - @CostTerraniteCave;
    warp "012-3.gat",445,65;
    goto L_Clearvars;

L_TravelTulimshar:
    if (!(TravelFound & $@tulimshar_bit))
        goto L_NoFound;
    if (Zeny < @CostTulimshar)
        goto L_NoMoney;
    mes "["+@NpcName$+"]";
    mes "\"Be fearless!\"";
    close2;
    set Zeny, Zeny - @CostTulimshar;
    warp "001-1.gat",44,70;
    goto L_Clearvars;

L_TravelBlueSage:
    if (!(TravelFound & $@blue_sage_bit))
        goto L_NoFound;
    if (Zeny < @CostBlueSage)
        goto L_NoMoney;
    mes "["+@NpcName$+"]";
    mes "\"Be fearless!\"";
    close2;
    set Zeny, Zeny - @CostBlueSage;
    warp "048-1.gat",65,45;
    goto L_Clearvars;

L_TravelHurnscald:
    if (!(TravelFound & $@hurnscald_bit))
        goto L_NoFound;
    if (Zeny < @CostHurnscald)
        goto L_NoMoney;
    mes "["+@NpcName$+"]";
    mes "\"Be fearless!\"";
    close2;
    set Zeny, Zeny - @CostHurnscald;
    warp "008-1.gat",79,84;
    goto L_Clearvars;

L_TravelNo:
    mes "["+@NpcName$+"]";
    mes "\"Perhaps some day you will have the courage to help us.  Enjoy your stay here in Hurnscald.\"";
    close2;
    goto L_Clearvars;

L_NoMoney:
    mes "["+@NpcName$+"]";
    mes "\"Sorry, you don't have enough money.  Maybe next time.\"";
    close2;
    goto L_Clearvars;

L_NoFound:
    mes "["+@NpcName$+"]";
    mes "\"Sorry, you don't have a memory from that location.  Maybe next time.\"";
    close2;
    goto L_Clearvars;

L_AlreadyThere:
    mes "["+@NpcName$+"]";
    mes "\"Umm, you are already there. Are you sure you know where you are going?\"";
    close2;
    goto L_Clearvars;

L_Clearvars:
    set @CostDruidTree, 0;
    set @CostGraveyard, 0;
    set @CostMagicHouse, 0;
    set @CostTerraniteCave, 0;
    set @CostTulimshar, 0;
    set @CostBlueSage, 0;
    return;
}
