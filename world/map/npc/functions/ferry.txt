// The ferry system

botcheck.gat,27,28,0|script|#FerryConfig|-1,
{
OnInit:
    disablenpc "#HurnscaldKoga";
    disablenpc "#HurnscaldFerry";
    disablenpc "#CandorKoga";
    disablenpc "#CandorFerry";
    disablenpc "#NivalisKoga";
    disablenpc "#NivalisFerry";
    disablenpc "#TulCandorKoga";
    disablenpc "#TulCandorFerry";
    disablenpc "#TulimsharKoga";
    disablenpc "#TulimsharFerry";
    set $@MainCurrentDock, 0;
    set $@CandorCurrentDock, 0;
    set $@DockTickCount, 0;
    set $@DockLeaveCount, 1;
    setarray $@MainDocks$, "Hurnscald", "Nivalis", "Tulimshar";
    setarray $@CandorDocks$, "Candor", "TulCandor";
    cmdothernpc "#"+$@MainDocks$[$@MainCurrentDock]+"Dock", "Arrive";
    cmdothernpc "#"+$@CandorDocks$[$@CandorCurrentDock]+"Dock", "Arrive";
    initnpctimer;
    end;

L_NextDock:
    set $@MainLastDock, $@MainCurrentDock;
    set $@CandorLastDock, $@CandorCurrentDock;
    set $@MainCurrentDock, $@MainCurrentDock + 1;
    set $@CandorCurrentDock, $@CandorCurrentDock + 1;
    if($@MainCurrentDock == getarraysize($@MainDocks$))
        set $@MainCurrentDock, 0;
    if($@CandorCurrentDock == getarraysize($@CandorDocks$))
        set $@CandorCurrentDock, 0;
    set $@DockTickCount, 0;
    cmdothernpc "#"+$@MainDocks$[$@MainLastDock]+"Dock", "Warp";
    cmdothernpc "#"+$@MainDocks$[$@MainCurrentDock]+"Dock", "Arrive";
    cmdothernpc "#"+$@CandorDocks$[$@CandorLastDock]+"Dock", "Warp";
    cmdothernpc "#"+$@CandorDocks$[$@CandorCurrentDock]+"Dock", "Arrive";
    initnpctimer;
    end;

OnTimer10000:
    if ($@DockTickCount > $@DockLeaveCount)
        goto L_NextDock;
    if ($@DockTickCount)
        cmdothernpc "#"+$@MainDocks$[$@MainCurrentDock]+"Dock", "Annouce";
        cmdothernpc "#"+$@CandorDocks$[$@CandorCurrentDock]+"Dock", "Annouce";
    set $@DockTickCount, $@DockTickCount + 1;
    initnpctimer;
    end;

}

function|script|BoardFerry|,
{
    mes "Board the ferry?";
    menu
        "yes.", L_Board,
        "no.", L_Return;

L_Board:
    warp "koga.gat",39,29;
    goto L_Return;

L_Return:
    return;
}

function|script|BoardCandorFerry|,
{
    mes "Board the ferry?";
    menu
        "yes.", L_Board,
        "no.", L_Return;

L_Board:
    if (BaseLevel < 40) goto L_TooYoung;
    warp "koga-candor.gat",39,29;
    goto L_Return;

L_TooYoung:
    mes "This area is too dangerous for your low level.";
    goto L_Return;

L_Return:
    return;
}
