// Debug Underworld/Alacrius CRYPT_Quest
// CRYPT_Quest
// NIBBLE_0 - Mainline
// Alacrius, Crypts Entrance 027-2
// 1 - Knows about items and where to get them
// Styx, Last Chance Island 070-1
// 2 - Investigated Reaper/Wife
// 3 - Beat Reaper Once
// NIBBLE_1 -
// Cerhan, GY Inn, 027-2
// 1 - Knows Materials
// 2 - Got Underworld Key
// --- Bound to UnderWorld ---
// X
// 3 - X talked to
// NIBBLE_2 -
// Markie Desod, Blue Sage Mansion, 048-2
// 1 - Knows Reagents
// 2 - Got Quill of Binding
// --- Bound to UnderWorld ---
// Y
// 3 - Y talked to
// NIBBLE_3 -
// Rygial, Tulimshar School of Magic, 024-2
// 1 - Knows Ingredients
// 2 - Got Blood Ink
// --- Bound to UnderWorld ---
// Z
// 3 - Z talked to
// NIBBLE 4
// Gate 
// 1 - Bound to Underworld
// NIBBLE 5
// Empty
// NIBBLE 6
// Empty
// NIBBLE 7
// Empty

function|script|UndeadDebug|,
{
    set @state, ((CRYPT_Quest & NIBBLE_0_MASK) >> NIBBLE_0_SHIFT);
    set @craftkey, ((CRYPT_Quest & NIBBLE_1_MASK) >> NIBBLE_1_SHIFT);
    set @craftquill, ((CRYPT_Quest & NIBBLE_2_MASK) >> NIBBLE_2_SHIFT);
    set @craftink, ((CRYPT_Quest & NIBBLE_3_MASK) >> NIBBLE_3_SHIFT);
    set @gatebind, ((CRYPT_Quest & NIBBLE_4_MASK) >> NIBBLE_4_SHIFT);

L_Main:
    if (@undeaddebug == 1)
        goto L_UndeadDebugOne;
    if (@undeaddebug == 2)
        goto L_UndeadDebugTwo;
    if (@undeaddebug == 3)
        goto L_UndeadDebugThree;
    if (@undeaddebug == 4)
        goto L_UndeadDebugFour;
    if (@undeaddebug == 5)
        goto L_UndeadDebugFive;
    goto L_Close;

L_UndeadDebugOne:
    mes "Reset your self to the various states.";
    mes "Options Limited to Time and Place.";
    menu
        "Audit Quest", L_AuditQuest,
        "Alacrius Unknown", L_Reset,
        "Alacrius Known", L_QuestStart,
        "Underworld Key known", L_KnowKey,
        "Underworld Key Mats", L_MatsKey,
        "Underworld Key made", L_CraftKey,
        "Quill of Binding known", L_KnowQuill,
        "Quill of Binding Mats", L_MatsQuill,
        "Quill of Binding made", L_CraftQuill,
        "Blood Ink known", L_KnowInk,
        "Blood Ink mats", L_MatsInk,
        "Blood Ink made", L_CraftInk,
        "Book of Dead", L_BookOfDead,
        "Nevermind.", L_Close;

L_UndeadDebugTwo:
    mes "Reset your self to the various states.";
    mes "Options Limited to Time and Place.";
    menu
        "Audit Quest", L_AuditQuest,
        "Set Boss Points", L_AddBossPoints,
        "Book of Dead", L_BookOfDead,
        "Bound to Underworld", L_BindSoul,
        "Escape to GY Inn.", L_Warp,
        "Nevermind.", L_Close;

L_UndeadDebugThree:
    mes "Reset your self to the various states.";
    mes "Options Limited to Time and Place.";
    menu
        "Get Ritual Items.", L_KrukanItems,
        "Nevermind.", L_Close;

L_UndeadDebugFour:
    mes "Reset your self to the various states.";
    mes "Options Limited to Time and Place.";
    menu
        "Get Ritual Items.", L_RazhaItems,
        "Nevermind.", L_Close;

L_UndeadDebugFive:
    mes "Reset your self to the various states.";
    mes "Options Limited to Time and Place.";
    menu
        "Get Ritual Items.", L_TeroganItems,
        "Nevermind.", L_Close;

L_AuditQuest:
    mes "Boss Points:         " + BOSS_POINTS;
    mes "NIBBLE_0 Mainline:   " + @state;
    mes "NIBBLE_1 Key/Talk:   " + @craftkey;
    mes "NIBBLE_2 Quill/Talk: " + @craftquill;
    mes "NIBBLE_3 Ink/Talk:   " + @craftink;
    mes "NIBBLE_4 Gate:       " + @gatebind;
    goto L_Main;

L_Reset:
    set @state, 0;
    set @craftkey, 0;
    set @craftquill, 0;
    set @craftink, 0;
    set @gatebind, 0;
    delitem "UnderworldKey", 1;
    delitem "QuillOfBinding", 1;
    delitem "BloodInk", 1;
    callsub S_Update_Var;
    goto L_Warp;

L_QuestStart:
    set @state, 1;
    set @craftkey, 0;
    set @craftquill, 0;
    set @craftink, 0;
    set @gatebind, 0;
    delitem "UnderworldKey", 1;
    delitem "QuillOfBinding", 1;
    delitem "BloodInk", 1;
    callsub S_Update_Var;
    goto L_Warp;

L_KnowKey:
    set @state, 1;
    set @craftkey, 1;
    set @gatebind, 0;
    callsub S_Update_Var;
    goto L_Warp;

L_MatsKey:
    getitem "CryptKey", 10;
    getitem "Bone", 25;
    getitem "Skull", 25;
    getitem "Soul", 5;
    set Zeny, Zeny + 20000;
    goto L_Warp;

L_CraftKey:
    set @state, 1;
    set @craftkey, 2;
    set @gatebind, 0;
    getinventorylist;
    if ((checkweight("UnderworldKey", 1) == 0) || (@inventorylist_count == 100))
        goto L_InventoryNoSpace;
    getitem "UnderworldKey", 1;
    callsub S_Update_Var;
    goto L_Warp;

L_KnowQuill:
    set @state, 1;
    set @craftquill, 1;
    set @gatebind, 0;
    callsub S_Update_Var;
    goto L_Warp;

L_MatsQuill:
    getitem "RedDye", 10;
    getitem "YellowDye", 10;
    getitem "WhiteFur", 20;
    getitem "YetiClaw", 20;
    set Zeny, Zeny + 20000;
    goto L_Warp;

L_CraftQuill:
    set @state, 1;
    set @craftquill, 2;
    set @gatebind, 0;
    getinventorylist;
    if ((checkweight("QuillOfBinding", 1) == 0) || (@inventorylist_count == 100))
        goto L_InventoryNoSpace;
    getitem "QuillOfBinding", 1;
    callsub S_Update_Var;
    goto L_Warp;

L_KnowInk:
    set @state, 1;
    set @craftink, 1;
    set @gatebind, 0;
    callsub S_Update_Var;
    goto L_Warp;

L_MatsInk:
    getitem "BlackScorpionStinger", 20;
    getitem "MountainSnakeEgg", 10;
    getitem "MaggotSlime", 20;
    getitem "JarofOwnBlood", 5;
    set Zeny, Zeny + 20000;
    goto L_Warp;

L_CraftInk:
    set @state, 1;
    set @craftink, 2;
    set @gatebind, 0;
    getinventorylist;
    if ((checkweight("BloodInk", 1) == 0) || (@inventorylist_count == 100))
        goto L_InventoryNoSpace;
    getitem "BloodInk", 1;
    callsub S_Update_Var;
    goto L_Warp;

L_InventoryNoSpace:
    mes "\"Drop some weight then come back.\"";
    goto L_Close;

L_AddBossPoints:
    mes "Set Boss Points.";
    input @bosspoints;
    set BOSS_POINTS, @bosspoints;
    set @bosspoints, 0;
    goto L_Main;

L_BookOfDead:
    set @gatebind, 0;
    callsub S_Update_Var;
    callfunc "ClearVariables";
    set @map$, "009-1.gat";
    setarray @Xs, 52, 53, 54, 52, 54, 52, 54;
    setarray @Ys, 39, 39, 39, 40, 40, 41, 41;
    set @x, 0;
    set @y, 0;
    set @warpmap$, "027-4.gat";
    set @warpx, 108;
    set @warpy, 88;
    goto L_SaveWarp;

L_BindSoul:
    callfunc "ClearVariables";
    set @gatebind, 1;
    callsub S_Update_Var;
    set @map$, "070-1.gat";
    setarray @Xs, 55, 56, 57, 55, 57, 55, 57;
    setarray @Ys, 70, 70, 70, 71, 71, 72, 72;
    set @x, 0;
    set @y, 0;
    set @warpmap$, "070-1.gat";
    set @warpx, 33;
    set @warpy, 71;
    goto L_SaveWarp;

L_Warp:
    set @gatebind, 0;
    callsub S_Update_Var;
    callfunc "ClearVariables";
    set @map$, "009-1.gat";
    setarray @Xs, 52, 53, 54, 52, 54, 52, 54;
    setarray @Ys, 39, 39, 39, 40, 40, 41, 41;
    set @x, 0;
    set @y, 0;
    set @warpmap$, "027-2.gat";
    set @warpx, 123;
    set @warpy, 102;
    goto L_SaveWarp;

L_SaveWarp:
    if (@x == 0 && @y == 0)
        goto L_FindPointWarp;

L_FindPointWarp:
    set @n, rand(getarraysize(@Xs));
    set @x, @Xs[@n];
    set @y, @Ys[@n];
    goto L_DoSaveWarp;

L_DoSaveWarp:
    savepoint @map$, @x, @y;
    goto L_ReturnWarp;

L_ReturnWarp:
    warp @warpmap$,@warpx,@warpy;
    goto L_Close;

L_KrukanItems:
    getitem "Soul", 1;
    getitem "Skull", 5;
    getitem "DarkCrystal", 5;
    getitem "Bone", 5;
    goto L_Main;

L_RazhaItems:
    getitem "Soul", 3;
    getitem "RottenRags", 5;
    getitem "UndeadEye", 5;
    getitem "UndeadEar", 5;
    goto L_Main;

L_TeroganItems:
    getitem "Soul", 5;
    getitem "DiseasedHeart", 5;
    getitem "UndeadEye", 5;
    getitem "UndeadEar", 5;
    goto L_Main;

L_Close:
    set @map$, "";
    cleararray @Xs[0], 0, 7;
    cleararray @Ys[0], 0, 7;
    set @x, 0;
    set @y, 0;
    set @state, 0;
    set @craftkey, 0;
    set @craftquill, 0;
    set @craftink, 0;
    set @gatebind, 0;
    close;

S_Update_Var:
    set CRYPT_Quest, (CRYPT_Quest & ~(NIBBLE_0_MASK) | (@state << NIBBLE_0_SHIFT));
    set CRYPT_Quest, (CRYPT_Quest & ~(NIBBLE_1_MASK) | (@craftkey << NIBBLE_1_SHIFT));
    set CRYPT_Quest, (CRYPT_Quest & ~(NIBBLE_2_MASK) | (@craftquill << NIBBLE_2_SHIFT));
    set CRYPT_Quest, (CRYPT_Quest & ~(NIBBLE_3_MASK) | (@craftink << NIBBLE_3_SHIFT));
    set CRYPT_Quest, (CRYPT_Quest & ~(NIBBLE_4_MASK) | (@gatebind << NIBBLE_4_SHIFT));
    return;
}

// Debug for Crypts
027-2.gat,120,103,0|script|UndeadDebug1|155,
{
    set @undeaddebug, 1;
    callfunc "UndeadDebug";
    end;   
}

// Debug for underworld
070-1.gat,32,62,0|script|UndeadDebug2|155,
{
    set @undeaddebug, 2;
    callfunc "UndeadDebug";
    end;   
}

// Debug for Krukan
027-3.gat,84,89,0|script|UndeadDebug3|155,
{
    set @undeaddebug, 3;
    callfunc "UndeadDebug";
    end;   
}

// Debug for Razha
027-4.gat,76,79,0|script|UndeadDebug4|155,
{
    set @undeaddebug, 4;
    callfunc "UndeadDebug";
    end;   
}

// Debug for Terogan
027-5.gat,72,26,0|script|UndeadDebug5|155,
{
    set @undeaddebug, 5;
    callfunc "UndeadDebug";
    end;   
}
