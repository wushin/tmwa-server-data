// Annual Xmas
// author: Jenalya, wushin
// Osmo asks for some White Fur which is used for bedding some presents in the boxes
// can be sabotaged by given Cloth instead

030-2.gat,37,77,0|script|Osmo|328,
{
    callfunc "XmasTime";

    if(@xmas_time == 1)
        goto L_XmasMain;
    goto L_OffSeason;

L_OffSeason:
    mes "[Osmo]";
    mes "\"It's nice to relax and not have to worry about Xmas.\"";
    mes "\"All that rush to gather those furs kills me every year.\"";
    goto L_Close;

L_XmasMain:
    if (XMASTIME & (1 << 16)) goto L_Done;
    if (XMASTIME & (1 << 17)) goto L_Bedding;
    if (XMASTIME & (1 << 30)) goto L_Helping;

    mes "[Osmo]";
    mes "\"What? Uh, excuse me, I have some work to do.\"";
    goto L_Close;

L_Helping:
    mes "[Osmo]";
    mes "\"You came in at just the right moment. I was informed that our bedding material for fragile presents is nearly depleted.";
    mes "Could you bring me " + $@bedding_amount + " White Furs?\"";
    next;
    mes "[Osmo]";
    mes "\"Take care that it's real White Fur.";
    mes "Other materials tend to go out of place in the present box, leaving the fragile content unprotected from any pushes.\"";
    set XMASTIME, XMASTIME | (1 << 17);
    goto L_Close;

L_Bedding:
    mes "[Osmo]";
    mes "\"Ah, you're back. Do you have the " + $@bedding_amount + " White Fur needed as bedding for fragile presents?\"";
    if ((countitem("WhiteFur") >= $@bedding_amount) && (countitem("CottonCloth") >= $@bedding_amount))
        menu
            "Yeah, here it is. (Give the White Fur.)",L_Fur,
            "Sure, here it is. (Give the Cotton Cloth.)",L_Cloth,
            "No, I'm still working on that.",L_Close;
    if (countitem("WhiteFur") >= $@bedding_amount)
        menu
            "Yeah, here it is. (Give the White Fur.)",L_Fur,
            "No, I'm still working on that.",L_Close;
    if (countitem("CottonCloth") >= $@bedding_amount)
        menu
            "Sure, here it is. (Give the Cotton Cloth.)",L_Cloth,
            "No, I'm still working on that.",L_Close;
    goto L_Close;

L_Fur:
    if (countitem("WhiteFur") < $@bedding_amount)
        goto L_NoItems;
    delitem "WhiteFur", $@bedding_amount;
    set XMASTIME, XMASTIME | (1 << 16);
    set @karma_bonus, 1;
    callfunc "XmasNice";
    mes "[Osmo]";
    mes "\"Wonderful! Thanks for your effort.\"";
    goto L_Close;

L_Cloth:
    if (countitem("CottonCloth") < $@bedding_amount)
        goto L_NoItems;
    delitem "CottonCloth", $@bedding_amount;
    set XMASTIME, XMASTIME | (1 << 16);
    set @karma_bonus, 1;
    callfunc "XmasNaughty";
    mes "He doesn't notice that you're giving him Cotton Cloth.";
    mes "[Osmo]";
    mes "\"Wonderful! Thanks for your effort.\"";
    goto L_Close;

L_Done:
    mes "[Osmo]";
    if (XMASTIME & (1 << 17))
        mes "\"The Fur you brought me isn't working very well.\"";
    if (!(XMASTIME & (1 << 17)))
        mes "\"Thanks for helping out with the bedding material.\"";
    goto L_Close;

L_NoItems:
    mes "[Osmo]";
    mes "\"Where do you have it? Is this a joke?\"";
    goto L_Close;

L_Close:
    close;
}
