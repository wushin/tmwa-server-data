// Annual Xmas
// author: Jenalya, wushin

030-2.gat,89,23,0|script|Verneri|334,
{
    callfunc "XmasTime";

    if(@xmas_time == 2)
        goto L_OffSeason;
    if(@xmas_state > 3)
        goto L_XmasMain;
    goto L_OffSeason;

L_OffSeason:
    mes "[Verneri]";
    mes "\"Nothing like the getting past the holiday rush to make you dread next season.\"";
    goto L_Close;

L_XmasMain:
    mes "[Verneri]";
    mes "\"Hm? I'm currently having a break. It's really nice here at the fireplace, isn't it?\"";
    next;
    mes "\"Ah, I remember when I was a child we used to roast acorns at the fireplace. We dunked them into Iron Potion and then put them over the fire for a while. That gave a tasty icing.\"";
    menu
        "Let's roast some.", -,
        "Children's stuff...", L_Close;

    mes "[Verneri]";
    mes "\"Well, if you get some Acorns and Iron Potions, we can do that.\"";
    mes "\"With one Iron Potion we can ice about " + $@acornAmount + " Acorns.\"";
    next;
    mes "[Verneri]";
    mes "\"Do you have everything we need? Shall we start?\"";
    menu
        "Sure!", L_Roast,
        "I need to get the ingredients.", L_Close;

L_Roast:
    if (countitem("Acorn") < $@acornAmount || countitem("IronPotion") < 1)
        goto L_NoItems;

    delitem "IronPotion", 1;
    delitem "Acorn", 1;

    mes "Verneri takes the Iron Potion and the Acorns from you. He pours the potion into some bowl and adds the Acorns.";
    mes "Then he puts the bowl over the fire, stirring it every now and then. After a while it starts to smell really tasty.";
    next;
    mes "[Verneri]";
    mes "\"Here we are! Let me take one of them, ok?\"";
    mes "He carefully takes one of the roasted acorns and eats it.";
    next;

    mes "[Verneri]";
    mes "\"Ouch, hot. But tasty! Some of them might be burnt, I'll throw those away.\"";
    set XMASTIME, XMASTIME | (1 << 21);
    set @karma_bonus, 1;
    callfunc "XmasNice";
    getitem "RoastedAcorn", rand($@acornAmount);
    menu
        "Let's roast some more.", L_Roast,
        "Thank you.", L_Close;

L_RoastAcornAgain:
    mes "[Verneri]";
    mes "\"Hello again. Do you want to roast some more acorns? They're delicious, right? Maybe we should do this every year.\"";
    menu
        "Not now.", L_Close,
        "Yes, I brought the ingredients.", L_Roast;

L_NoItems:
    mes "[Verneri]";
    mes "\"You need to bring at least one Iron Potion and " + $@acornAmount + " Acorns.\"";
    goto L_Close;

L_Close:
    close;
}
