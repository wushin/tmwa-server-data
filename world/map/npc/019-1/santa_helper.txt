// Author: accoring to git history probably Jaxad
// and extended by wushin to be included in the annual
// quest made seasonal and adapted to new scripting standards by Jenalya

019-1.gat,74,75,0|script|Santa's Helper|105,
{
    callfunc "ClearVariables";
    callfunc "XmasStates";

    if (($@xmas_time) && (@sh_cap)) goto L_Done;
    if (($@xmas_time) && (@sh_sweater)) goto L_Continue;
    if ($@xmas_time) goto L_Quest;

    mes "[Santa's Helper]";
    mes "\"Ah, isn't it peaceful here? Around Christmas time we always have problems with some slimy monsters.";
    mes "They steal the presents boxes. I think they feel attracted by the spiritual power of the Christmas presents.\"";
    next;
    mes "He smiles happily.";
    mes "[Santa's Helper]";
    mes "\"But at this time of the year everything's calm and relaxing.\"";
    goto L_Close;

L_Quest:
    mes "[Santa's Helper]";
    mes "\"Hello my young friend.";
    mes "Would you like to help me?\"";
    next;
    mes "[Santa's Helper]";
    mes "\"Those monsters stole Santa's presents.";
    mes "If you help me, I'll give you something very nice\"";
    next;

L_M:
    menu
        "I have some present boxes", L_Y,
        "Hmm, see you later", L_N,
        "What you need exactly?", L_R;

L_Y:
    mes "[Santa's Helper]";
    mes "\"Hmm, let me check what you have.\"";
    next;
    if (countitem("PurplePresentBox") < $@sh_purple_amount)
        goto L_NoItem;
    if (countitem("BluePresentBox") < $@sh_blue_amount)
        goto L_NoItem;
    if (countitem("GreenPresentBox") < $@sh_green_amount)
        goto L_NoItem;
    getinventorylist;
    if (@inventorylist_count > 99)
        goto L_TooMany;
    mes "[Santa's Helper]";
    mes "\"Great! Here is something for you\"";
    delitem "PurplePresentBox", $@sh_purple_amount;
    delitem "BluePresentBox", $@sh_blue_amount;
    delitem "GreenPresentBox", $@sh_green_amount;
    if (@sh_sweater)
        goto L_KnitCap;
    getitem "TurtleneckSweater", 1;
    set XMASTIME, XMASTIME | $@sh_bit_sweater;
    set @karma_bonus, $default_karma_bonus;
    callfunc "XmasNice";
    goto L_Close;

L_KnitCap:
    getitem "KnitCap", 1;
    set XMASTIME, XMASTIME | $@sh_bit_cap;
    set @karma_bonus, $default_karma_bonus;
    callfunc "XmasNice";
    goto L_Close;

L_R:
    mes "[Santa's Helper]";
    mes "\"I need:";
    mes $@sh_purple_amount + " Purple Present Boxes";
    mes $@sh_blue_amount + " Blue Present Boxes";
    mes $@sh_green_amount + " Green Present Boxes\"";
    next;
    goto L_M;

L_N:
    mes "[Santa's Helper]";
    mes "\"Enjoy your holidays and I wish you a happy Christmas!\"";
    goto L_Close;

L_NoItem:
    mes "[Santa's Helper]";
    mes "\"You dont seem to have enough presents.\"";
    goto L_Close;

L_Continue:
    mes "[Santa's Helper]";
    mes "\"Those Nasty Monsters have stolen my presents again!\"";
    next;
    mes "Would you like to help me?\"";
    mes "If you help me, I'll give you something very nice\"";
    next;

L_ContinueMenu:
    menu
        "I have some present boxes", L_Y,
        "Hmm, see you later", L_N,
        "What you need exactly?", L_R;
    
L_Done:
    mes "[Santa's Helper]";
    mes "\"All the children got their Christmas presents.";
    mes "Thank you for your help.\"";
    goto L_Close;

L_TooMany:
    mes "[Santa's Helper]";
    mes "\"It doesn't look like you have room for this. Come back later.\"";
    goto L_Close;

L_Close:
    close;
}
