// Government official taking care about shop licenses
// involved in quest given by 021-1/imec.txt and 021-2/kylian.txt

001-2,31,21,0|script|Yanis|107
{
    set QUEST_NorthTulimshar, QUEST_NorthTulimshar | $@knowYanisNT;
    set @halloween_npc_id, $@halloween_npc_yanis;
    callfunc "TrickOrTreat";

    goto L_Base_Menu;

L_Base_Menu:

    mes "[Yanis]";
    mes "\"Welcome. I'm handling issues with trading licenses for the shop owners in Tulimshar. Can I help you?\"";
    if (QL_IMEC != 1)
        menu
            "Nothing right now.",L_BeforeClose;
    menu
        "Imec asked me to bring this petition.",L_Next,
        "Nothing right now.",L_BeforeClose;

L_Next:
    mes "[Yanis]";
    mes "\"Imec? I see. Give it to me.\"";
    mes "Yanis has a disapproving look on his face. He takes the letter and writes something on another paper.";
    next;
    mes "[Yanis]";
    mes "\"Here you have a letter of acknowledgement. You can bring that back to Imec. We will have a look at his case.\"";
    set QL_IMEC, 2;
    goto L_BeforeClose;

L_BeforeClose:
    if (QL_KYLIAN != 3)
        goto L_Close;
    next;
    mes "You think that this is the person Kylian needs to talk to. You should tell him.";
    goto L_Close;

L_Close:
    close;
}
