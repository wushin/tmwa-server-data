// Debug for underworld

070-1.gat,32,62,0|script|UndeadDebug|155,
{
    set @state, ((GATE_Instance & NIBBLE_0_MASK) >> NIBBLE_0_SHIFT);

    mes "Reset your self to the various states.";
    mes "Legend: (Number) is Quest Var, [W] warps.";
    mes "[BS] Changes Soulmenhir. [UBS] Unbinds it.";
    mes "Are you sure you are ready to go?";
    menu
        "Add Boss Points", L_AddBossPoints,
        "Book of Dead (0) [W] [UBS]", L_BookOfDead,
        "Bound to Underworld (1) [BS]", L_BindSoul,
        "Escape to GY Inn. (0) [W] [UBS]", L_Warp;

L_AddBossPoints:
    mes "Add How Many Boss Points?";
    input @bosspoints;
    set BOSS_POINTS, @bosspoints;
    set @bosspoints, 0;
    goto L_Close;

L_BindSoul:
    callfunc "ClearVariables";
    set @state, 1;
    callsub S_Update_Var;
    set @map$, "070-1.gat";
    setarray @Xs, 55, 56, 57, 55, 57, 55, 57;
    setarray @Ys, 70, 70, 70, 71, 71, 72, 72;
    set @x, 0;
    set @y, 0;
    set @warpmap$, "070-1.gat";
    set @warpx, 33;
    set @warpy, 71;
    goto L_SaveWarp;

L_BookOfDead:
    set @state, 0;
    callsub S_Update_Var;
    callfunc "ClearVariables";
    set @map$, "009-1.gat";
    setarray @Xs, 52, 53, 54, 52, 54, 52, 54;
    setarray @Ys, 39, 39, 39, 40, 40, 41, 41;
    set @x, 0;
    set @y, 0;
    set @warpmap$, "027-4.gat";
    set @warpx, 108;
    set @warpy, 88;
    goto L_SaveWarp;

L_Warp:
    set @state, 0;
    callsub S_Update_Var;
    callfunc "ClearVariables";
    set @map$, "009-1.gat";
    setarray @Xs, 52, 53, 54, 52, 54, 52, 54;
    setarray @Ys, 39, 39, 39, 40, 40, 41, 41;
    set @x, 0;
    set @y, 0;
    set @warpmap$, "027-2.gat";
    set @warpx, 105;
    set @warpy, 44;
    goto L_SaveWarp;

L_SaveWarp:
    if (@x == 0 && @y == 0)
        goto L_FindPointWarp;

L_DoSaveWarp:
    savepoint @map$, @x, @y;
    goto L_ReturnWarp;

L_FindPointWarp:
    set @n, rand(getarraysize(@Xs));
    set @x, @Xs[@n];
    set @y, @Ys[@n];
    goto L_DoSaveWarp;

L_ReturnWarp:
    warp @warpmap$,@warpx,@warpy;
    goto L_Close;

L_Close:
    set @map$, "";
    cleararray @Xs[0], 0, 7;
    cleararray @Ys[0], 0, 7;
    set @x, 0;
    set @y, 0;
    set @state, 0;
    close;

S_Update_Var:
    set GATE_Instance, (GATE_Instance & ~(NIBBLE_0_MASK) | (@state << NIBBLE_0_SHIFT));
    return;
}

